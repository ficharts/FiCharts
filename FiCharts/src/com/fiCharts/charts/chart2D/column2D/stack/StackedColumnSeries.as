package com.fiCharts.charts.chart2D.column2D.stack
{
	import com.fiCharts.charts.chart2D.column2D.Column2DUI;
	import com.fiCharts.charts.chart2D.column2D.ColumnSeries2D;
	import com.fiCharts.charts.chart2D.core.axis.LinearAxis;
	import com.fiCharts.charts.chart2D.core.itemRender.PointRenderBace;
	import com.fiCharts.charts.chart2D.core.model.Chart2DModel;
	import com.fiCharts.charts.chart2D.encry.SB;
	import com.fiCharts.charts.common.ChartColors;
	import com.fiCharts.charts.common.Model;
	import com.fiCharts.charts.common.SeriesDataPoint;
	import com.fiCharts.charts.legend.model.LegendVO;
	import com.fiCharts.charts.legend.view.LegendEvent;
	import com.fiCharts.utils.XMLConfigKit.XMLVOLib;
	import com.fiCharts.utils.XMLConfigKit.XMLVOMapper;
	import com.fiCharts.utils.XMLConfigKit.style.LabelStyle;
	
	/**
	 * å †ç§¯åºåˆ—ç¾¤ç»„ç±»ï¼›
	 * 
	 * å †ç§¯åºåˆ—çš„å­åºåˆ—æ˜¯ä¸ªä»…æ‹¥æœ‰æ•°æ®çš„ç©ºå£³ï¼Œæ ·å¼å®šä¹‰ï¼Œæ ·å¼æ¸²æŸ“å…¨éƒ¨åœcode>StackedColumnSeries</code>ä¸­è¿›è¡Œï¼Œ 
	 * 
	 */	
	public class StackedColumnSeries extends ColumnSeries2D
	{
		public function StackedColumnSeries()
		{
			super();
		}
		
		/**
		 */		
		override protected function get type():String
		{
			return "stackedColumn";
		}
		
		/**
		 */		
		override public function beforeUpdateProperties(xml:*=null):void
		{
			XMLVOMapper.fuck(XMLVOLib.getXML(Chart2DModel.SERIES_DATA_STYLE, Model.SYSTEM), this);
			XMLVOMapper.fuck(XMLVOLib.getXML(Chart2DModel.STACKED_COLUMN_SERIES, Model.SYSTEM), this);
			
			this.stacks = new Vector.<StackedSeries>;
		}
		
		/**
		 */		
		override protected function draw():void
		{
			if (ifDataChanged)
			{
				while (canvas.numChildren)
					canvas.removeChildAt(0);
				
				var columnItemUI:Column2DUI;
				columnUIs = new Vector.<Column2DUI>;
				for each (var stack:StackedSeries in stacks)
				{
					for each (var itemDataVO:SeriesDataPoint in stack.dataItemVOs)
					{
						if (ifNullData(itemDataVO))
							continue;
						
						columnItemUI = getSeriesItemUI(itemDataVO);
						columnItemUI.states = this.states;//æ ·å¼ç»Ÿä¸€å®šä¹‰
						columnItemUI.metaData = itemDataVO.metaData;
						columnUIs.push(columnItemUI);
						canvas.addChild(columnItemUI);
					}
				}
			}
			
			if (this.ifSizeChanged || this.ifDataChanged)
			{
				layoutColumnUIs();
				ifDataChanged = ifSizeChanged = false;
			}
		}
		
		/**
		 */		
		override protected function layoutColumnUIs():void
		{
			var index:uint;
			for each (var columnUI:Column2DUI in this.columnUIs)
			{
				columnUI.x = columnUI.dataItem.x - partColumnWidth / 2;
				columnUI.y = columnUI.dataItem.y//verticalAxis.valueToY((columnUI.dataItem as StackedSeriesDataItem).startValue) - baseLine;
				setColumnUISize(columnUI);
				columnUI.render();
			}
		}
		
		/**
		 */		
		override protected function setColumnUISize(columnUI:Column2DUI):void
		{
			(columnUI.dataItem as StackedSeriesDataPoint).width = columnUI.columnWidth = partColumnWidth;
			(columnUI.dataItem as StackedSeriesDataPoint).height = columnUI.columnHeight = 
			
				verticalAxis.valueToY((columnUI.dataItem as StackedSeriesDataPoint).endValue) -
				verticalAxis.valueToY((columnUI.dataItem as StackedSeriesDataPoint).startValue);
		}
		
		/**
		 */		
		override protected function getSeriesItemUI(dataItem:SeriesDataPoint):Column2DUI
		{
			return new StackedColumnUI(dataItem);
		}
		
		/**
		 */		
		override public function layoutDataItems(startIndex:int, endIndex:int, step:uint = 1):void
		{
			adjustColumnWidth();
			
			var item:SeriesDataPoint;
			var i:int;
			
			for (i = startIndex; i <= endIndex; i += step)
			{
				item = dataItemVOs[i];
				
				item.x = horizontalAxis.valueToX(item.xValue, i) - columnGoupWidth / 2 +
					this.columnSeriesIndex * (partColumnWidth + columnGroupInnerSpaceUint) + partColumnWidth / 2;
				item.dataItemX = item.x;
				
				// æ•°æ®èŠ‚ç‚¹çš„åæ ‡ç³»ä¸æ¸²æŸ“èŠ‚ç‚¹ä¸åŒï¼Œ ä¸¤è€…ç›¸å·å€¼ä¸º baseLine
				item.y = verticalAxis.valueToY((item as StackedSeriesDataPoint).startValue) - baseLine;
				item.dataItemY = verticalAxis.valueToY((item as StackedSeriesDataPoint).endValue);
				item.offset = baseLine;
			}
			
			if(fullDataItems == null) return;//ç™¾åˆ†ç™¾å †ç§¯å›¾æ²¡æœ‰æ€»æ•°æ®èŠ‚ç‚
			
			for (i = startIndex; i <= endIndex; i += step)
			{
				item = fullDataItems[i];
				
				item.dataItemX = item.x = horizontalAxis.valueToX(item.xValue, i) - columnGoupWidth / 2 +
					this.columnSeriesIndex * (partColumnWidth + columnGroupInnerSpaceUint) + partColumnWidth / 2;
				
				item.y = item.dataItemY = verticalAxis.valueToY(item.yValue);
			}
		}
		
		/**
		 * åˆ›å»ºæ±‡æ€»æ•°æ®èŠ‚ç‚¹çš„æ¸²æŸ“å™¨ï¼Œ è¿™é‡Œåˆ›å»ºçš„æ¸²æŸ“å™¨åªæ˜¯ç”¨äºæ±‡æ€»æ•°æ®çš„æ˜¾ç¤ºï¼
		 */		
		override public function createItemRenders():void
		{
			super.createItemRenders();
			
			for each (var item:SeriesDataPoint in fullDataItems)
				initItemRender(combileItemRender, item);
		}
		
		/**
		 */		
		override protected function initItemRender(itemRender:PointRenderBace, item:SeriesDataPoint):void
		{
			if (ifNullData(item))
				return;
			
			itemRender.itemVO = item;
			
			item.metaData.valueLabel = item.yLabel;
			itemRender.value = value;
			
			// æ•´ä½“æ•°å€¼æ ·å¼ä¸ç‹¬ç«‹æ•°å€¼æ ·å¼æœ‰åˆ†åˆ«
			if (itemRender is StackedColumnCombiePointRender)
			{
				itemRender.valueLabel = this.valueLabel;
				this.updateLabelDisplay(itemRender);
			}
			else
			{
				itemRender.valueLabel = this.innerValueLabel;
				this.updateLabelDisplay(itemRender);
			}
			
			itemRender.dataRender = this.dataRender;
			itemRender.tooltip = this.tooltip;
			
			itemRender.initToolTips();
			itemRenders.push(itemRender);
		}
		
		/**
		 */		
		protected function get combileItemRender():PointRenderBace
		{
			return new StackedColumnCombiePointRender;
		}
		
		/**
		 * åºåˆ—æ•°æ®ï¼åæ ‡è½´æ•°æ®çš„åˆ›å»º
		 * 
		 * è¿™é‡ŒæŠŠåŸå§‹çš„æ•°æ®èŠ‚ç‚¹ä¸è®¡ç®—å¾—å‡ºçš„æ±‡æ€»æ•°æ®èŠ‚ç‚¹åˆ†ç¦»ï¼›
		 */		
		override protected function preInitData():void
		{
			var xValue:Object, yValue:Number, positiveValue:Number, negativeValue:Number;
			var length:uint = dataProvider.length;
			var stack:StackedSeries;
			var combleSeriesDataItem:SeriesDataPoint;
			var stackedSeriesDataItem:StackedSeriesDataPoint;
			
			dataItemVOs = new Vector.<SeriesDataPoint>
			horizontalValues = new Vector.<Object>;
			verticalValues = new Vector.<Object>;
			fullDataItems = new Vector.<SeriesDataPoint>;
			
			
			// å°†å­åºåˆ—çš„æ•°æ®èŠ‚ç‚¹åˆå¹¶åˆ°ä¸€èµ·ï¼›
			for each (stack in stacks)
			{
				stack.dataProvider = this.dataProvider;
				dataItemVOs = dataItemVOs.concat(stack.dataItemVOs);
			}
			
			// å°†å­åºåˆ—çš„æ•°å€¼å åŠ ï¼Œ å› åæ ‡è½´çš„æ•°å€¼æ˜¾ç¤ºçš„æ˜¯æ€»é‡ï¼
			for (var i:uint = 0; i < length; i++)
			{
				positiveValue = negativeValue = 0;
				for each (stack in stacks)
				{
					stackedSeriesDataItem = (stack.dataItemVOs[i] as StackedSeriesDataPoint);
					stackedSeriesDataItem.index = i;
					
					xValue = stackedSeriesDataItem.xValue;
					yValue = Number(stackedSeriesDataItem.yValue);
					
					if (yValue >= 0)
					{
						stackedSeriesDataItem.startValue = Number(positiveValue);
						positiveValue += yValue;
						stackedSeriesDataItem.endValue = Number(positiveValue);
					}
					else
					{
						stackedSeriesDataItem.startValue = Number(negativeValue);
						negativeValue += yValue;
						stackedSeriesDataItem.endValue = Number(negativeValue);
					}
					
					XMLVOMapper.pushAttributesToObject(stackedSeriesDataItem, stackedSeriesDataItem.metaData, 
						['startValue', 'endValue']);
				}
				
				// å°†æ‰€æœ‰å †ç§¯å€¼æ±‚å’Œï¼Œ æ•°å€¼æ ‡ç­¾å–æ±‚å’Œå€¼ï¼Œ æ ‡ç­¾çš„ä½ç½®å–å†³äºæœ€å€¼ï¼Œ 
				// æ±‚å’Œä¸ºæ­£å–æœ€å¤§å€¼ï¼Œ æ±‚å’Œä¸ºè´Ÿåˆ™å–æœ€å°å€¼ï¼›
				if (this.valueLabel.enable)
				{
					combleSeriesDataItem = new SeriesDataPoint();
					combleSeriesDataItem.index = i;
					combleSeriesDataItem.metaData = new Object;
					
					combleSeriesDataItem.xValue = xValue;
					combleSeriesDataItem.yValue = ((positiveValue + negativeValue) >= 0) ? positiveValue : negativeValue;
					
					combleSeriesDataItem.xLabel = horizontalAxis.getXLabel(combleSeriesDataItem.xValue);
					combleSeriesDataItem.yLabel = verticalAxis.getYLabel(positiveValue + negativeValue);
					
					combleSeriesDataItem.color = uint(this.color);
					combleSeriesDataItem.seriesName = this.seriesName;
					
					combleSeriesDataItem.xDisplayName = horizontalAxis.displayName;
					combleSeriesDataItem.yDisplayName = verticalAxis.displayName;
					
					XMLVOMapper.pushAttributesToObject(combleSeriesDataItem, combleSeriesDataItem.metaData, 
						['xValue', 'yValue', 'xLabel', 'yLabel', 'xDisplayName', 'yDisplayName', 'seriesName', 'color']);
					
					fullDataItems.push(combleSeriesDataItem); 
				}
				
				horizontalValues.push(xValue);
				verticalValues.push(positiveValue);
				verticalValues.push(negativeValue);
			}
			
			dataOffsetter.maxIndex = maxDataItemIndex = length - 1;
		}
		
		/**
		 */		
		override public function get legendData():Vector.<LegendVO>
		{
			var legendVOes:Vector.<LegendVO> = new Vector.<LegendVO>;
			var legendVO:LegendVO;
			
			for each(var item:StackedSeries in stacks)
			{
				legendVO = new LegendVO();
				legendVO.addEventListener(LegendEvent.LEGEND_ITEM_OVER, item.seriesLegendOverHandler, false, 0, true);
				legendVO.addEventListener(LegendEvent.LEGEND_ITEM_OUT, item.seriesLegendOutHandler, false, 0, true);
				
				legendVO.addEventListener(LegendEvent.HIDE_LEGEND, item.seriesHideLegendHandler, false, 0, true);
				legendVO.addEventListener(LegendEvent.SHOW_LEGEND, item.seriesShowLegendHandler, false, 0, true);
				
				legendVO.metaData = item;
				legendVOes.push(legendVO);
			}
				
			return legendVOes;
		}
		
		/**
		 */		
		override public function initSeriesName(index:uint):uint
		{
			var seriesIndex:uint = index;
			
			for each(var item:SB in stacks)
				seriesIndex = item.initSeriesName(seriesIndex)
				
			return seriesIndex;
		}
		
		/**
		 * 
		 */
		public function get stack():StackedSeries
		{
			return null;
		}

		public function set stack(value:StackedSeries):void
		{
			stacks.push(value);
		}

		/**
		 * å †ç§¯åºåˆ—çš„åˆ›å»ºä¸é…ç½®
		 */		
		override public function configed(colorMananger:ChartColors):void
		{
			if (this.labelDisplay != LabelStyle.NONE && verticalAxis is LinearAxis)
				(verticalAxis as LinearAxis).ifExpend = true;
			
			for each (var series:StackedSeries in this.stacks)
			{
				if (!series.color)// å¦‚æœæœªæŒ‡å®åºåˆ—é¢œè‰²åˆ™é‡‡ç”¨è‡ªåŠ¨åˆ†é…é¢œè‰
					series.color = colorMananger.chartColor.toString(16);
				
				series.horizontalAxis = this.horizontalAxis;
				series.verticalAxis = this.verticalAxis;
			}
			
			//stacks.reverse();
		}
		
		/**
		 */		
		protected var fullDataItems:Vector.<SeriesDataPoint>;
		
		/**
		 */		
		override protected function get itemRender():PointRenderBace
		{
			return new StackedColumnPointRender(false);
		}
		
		/**
		 */		
		protected var stacks:Vector.<StackedSeries>; 
		
	}
}